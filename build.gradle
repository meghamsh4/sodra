allprojects{
    apply plugin: 'java'
    apply plugin: 'jacoco'
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}   

repositories {
    mavenCentral()
    maven {
        url "http://maven.restlet.com"
    }
}

dependencies {
    compile group: 'org.apache.cassandra', name: 'cassandra-all', version: '2.2.2'
    compile group: 'org.apache.solr', name: 'solr-core', version: '5.3.1'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.11'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task codeCoverageReport(type: JacocoReport) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects 
    subprojects.each {
       sourceSets it.sourceSets.main
    }

    reports {
      xml.enabled true
      html.enabled true
      html.destination "${buildDir}/reports/jacoco"
      csv.enabled false
    }
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
}